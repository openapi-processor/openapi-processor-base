/*
 * Copyright 2020 https://github.com/openapi-processor/openapi-processor-base
 * PDX-License-Identifier: Apache-2.0
 */

package io.openapiprocessor.test

import java.nio.file.Path

/**
 * used to execute test sets.
 *
 * "expected" represents the files from the "outputs" folder of the test case
 * "generated" represents the files generated by the processor
 */
class TestSetRunner {
    Test test
    TestSet testSet

    TestSetRunner(Test test, TestSet testSet) {
        this.test = test
        this.testSet = testSet
    }

    /**
     * runs a test-set
     *
     * @return true on success, false on failure, i.e. if there were any differences
     */
    boolean run () {
        test.init()

        def mapping = test.mapping
        def options = [
            parser: test.parser,
            apiPath: test.apiPath.toString(),
            targetDir: test.targetDir.toString(),
            mapping: mapping.yaml
        ]

        when:
        testSet.processor.run (options)

        then:
        def expectedFiles = test.getExpectedFilePaths()
        def generatedFiles = test.getGeneratedFilePaths()
        generatedFiles = filterUnexpectedFiles(generatedFiles, expectedFiles)

        // check expected file names match generated file names
        assert expectedFiles.keySet() == generatedFiles.keySet()

        // compare expected files with the generated files
        def success = true
        expectedFiles.each {k, v ->
            def equalContent = !test.printUnifiedDiff (v, generatedFiles.get(k))
            success &= equalContent
        }

        success
    }

    Map<String, Path> filterUnexpectedFiles(Map<String, Path> generatedFiles, Map<String, Path> expectedFiles) {
        def unexpectedFiles = [
            "${test.packagePath}/support/Generated.java".toString(),
            "${test.packagePath}/validation/Values.java".toString(),
            "${test.packagePath}/validation/ValueValidator.java".toString()
        ] as Set<String>

        def expectedKeys = expectedFiles.keySet()

        def result = new TreeMap<String, Path>()

        generatedFiles.forEach {k, v ->
            def isExpected = expectedKeys.contains(k)
            def isUnexpected = unexpectedFiles.find { u -> k.endsWith(u) } != null

            if(!isExpected && isUnexpected) {
                return
            }

            result.put(k, v)
        }

        return result
    }
}
